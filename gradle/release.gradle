apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'HStreams'
                packaging 'jar'
                description 'Hibernate JDK 8 wrapper'
                url 'https://github.com/jmnarloch/hstreams'

                scm {
                    connection 'scm:git:https://github.com/jmnarloch/hstreams.git'
                    developerConnection 'scm:git:https://github.com/jmnarloch/hstreams.git'
                    url 'https://github.com/jmnarloch/hstreams.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jmnarloch'
                        name 'Jakub Narloch'
                        email 'jmnarloch@gmail.com'
                    }
                }
            }
        }
    }
}

def installer = install.repositories.mavenInstaller
def deployer = uploadArchives.repositories.mavenDeployer

[installer, deployer]*.pom*.whenConfigured {pom ->
    pom.dependencies.find {dep -> dep.groupId == 'org.hibernate'}.optional = true
}

release {
    tagTemplate = 'hstreams5-v${version}'

    git {
        requireBranch = 'hstreams-5'
        pushToRemote = 'origin'
        pushToCurrentBranch = false
    }
}

afterReleaseBuild.dependsOn uploadArchives